plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

group = 'pavelP'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation 'io.rest-assured:rest-assured:5.5.6'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    implementation 'com.github.javafaker:javafaker:1.0.2'
    testImplementation 'com.codeborne:selenide:6.19.1'
    testImplementation 'org.assertj:assertj-core:3.27.2'
    implementation 'org.aeonbits.owner:owner:1.0.12'

    testImplementation 'io.qameta.allure:allure-junit5:2.21.0'
    testImplementation 'io.qameta.allure:allure-rest-assured:2.21.0'
}

// === Общие настройки для всех тестов ===
tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging { events "PASSED", "FAILED", "SKIPPED" }

    // Параллельный запуск тестов JUnit 5
    systemProperties += [
            'junit.jupiter.execution.parallel.enabled'                 : 'true',
            'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
            'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
            'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
            'junit.jupiter.execution.parallel.config.fixed.parallelism': 4
    ]

    // Параллельные JVM Gradle
    maxParallelForks = 4
}

// === Задачи по тегам ===
tasks.register('testApi', Test) {
    useJUnitPlatform { includeTags 'API' }
    description = 'Запускает все тесты с тегом @Tag("API")'
    group = 'verification'
    maxParallelForks = 4
}

tasks.register('testWeb', Test) {
    useJUnitPlatform { includeTags 'WEB' }
    description = 'Запускает все тесты с тегом @Tag("WEB")'
    group = 'verification'
    maxParallelForks = 4
}

tasks.register('testSmoke', Test) {
    useJUnitPlatform { includeTags 'SMOKE' }
    description = 'Запускает все тесты с тегом @Tag("SMOKE")'
    group = 'verification'
    maxParallelForks = 4
}